plugins {
    id 'java'
    id 'idea'
//    id 'application'
    id 'com.google.protobuf' version '0.9.2'
}

group 'org.wly'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def grpcVersion = '1.46.1'

dependencies {
    runtimeOnly "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation 'org.mongodb:mongodb-driver-sync:4.9.0'
//    compileOnly 'org.apache.tomcat:tomcat-annotations-api:10.1.5'
//    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
//    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

sourceSets.main.java.srcDir new File(buildDir, 'generated/source')

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "greeting.client.GreetingClient"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


test {
    useJUnitPlatform()
}